#+TITLE:  齡一 / 1st-instar
#+AUTHOR: 謝宇恆 / XIE Yuheng
#+EMAIL:  xyheme@gmail.com

* ===================================
* note the order of development
** 1st-instar
   * indirect-threaded-code interpreter
     1. macro about argument_stack & return_stack
     2. macro about jo & jojo
     3. macro about next
     4. the way to do memory allocation
     5. begin_to_interpret_threaded_code
     6. little_test
* -----------------------------------
* note
** jo & jojo
   * use "jo" to denote bead
     and use "jojo" to denote a thread of beads
     [which reads like "珠珠" in Chinese]
** convention in assembly code
   * using underline to compose big word from small words
   * using "$" as prefix and postfix separator
   * indentation level = 3
   * naming convention of jo
     | convention   | jo type  |
     |--------------+----------|
     | prefix "V__" | variable |
     | prefix "M__" | macro    |
* ===================================
* prolog
** ----------------------------------
** choose your platform
   #+begin_src fasm :tangle cicada-nymph.fasm
   ;;;; before you compile the code
   ;;;; do not forget to choose your platform
   ;;;; in the following code

   linux? equ yes
   windows? equ no
   #+end_src
** misc
   #+begin_src fasm :tangle cicada-nymph.fasm
   ;; in fasm, "dup" is a reserved word
   dup equ duplicate

   ;; in fasm, "end" is a reserved word
   finish equ end
   end equ exit
   #+end_src
** ----------------------------------
** header [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   define sys_6_r8  r8
   define sys_5_r9  r9
   define sys_4_r10 r10
   define sys_3_rdx rdx
   define sys_2_rsi rsi
   define sys_1_rdi rdi
   define sys_n_rax rax

   ;; the only three linux syscall we will use
   define syscall_read    0
   define syscall_write   1
   define syscall_exit    60

   finish if
   #+end_src
** format header [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   format elf64 executable 3

   finish if
   #+end_src
** entry [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   entry begin_to_interpret_threaded_code
   segment readable executable writeable

   finish if
   #+end_src
** ----------------------------------
** header [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   STD_INPUT_HANDLE     = -10
   STD_OUTPUT_HANDLE    = -11

   define fun_4_r9   r9
   define fun_3_r8   r8
   define fun_2_rdx  rdx
   define fun_1_rcx  rcx

   finish if
   #+end_src
** format header [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   format PE64 console

   finish if
   #+end_src
** entry [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   entry begin_to_interpret_threaded_code
   section '.text' code writeable readable executable

   finish if
   #+end_src
** ----------------------------------
* -----------------------------------
* macro in assembly code
** jo_size
   * 64-bits
   * note that how "xx" is used as "dq" in a 64-bits version implementation
     when one wishs to port the implementation to 32-bits machine
     just use "xx equ dd"
   #+begin_src fasm :tangle cicada-nymph.fasm
   jo_size = 8 ;; (byte)
   xx equ dq
   #+end_src
** argument_stack & return_stack
   * when doing "push"
     a stack-pointer moves to lower address
   * note that another style is that
     when doing "push"
     a stack-pointer moves to higher address
   * the stack-pointer
     always stores the address of current-free-address of the stack
   * note that another style is that
     under the stack-pointer
     there always stores the value of the-top-of-the-stack
   #+begin_src fasm :tangle cicada-nymph.fasm
   ;; if you want to extend cicada in assembly
   ;; the following registers must NOT be used

   ;; =================================
   define pointer$argument_stack   r15
   define pointer$return_stack     r14
   ;; =================================

   macro push_argument_stack register {
      mov [pointer$argument_stack], register
      add pointer$argument_stack, jo_size
      }
   macro pop_argument_stack register {
      sub pointer$argument_stack, jo_size
      mov register, [pointer$argument_stack]
      }

   macro push_return_stack register {
      mov [pointer$return_stack], register
      add pointer$return_stack, jo_size
      }
   macro pop_return_stack register {
      sub pointer$return_stack, jo_size
      mov register, [pointer$return_stack]
      }
   #+end_src
** memory allocation in un_initialized_memory
   * implemented as a memory map
   #+begin_src fasm :tangle cicada-nymph.fasm
   current_free_address$un_initialized_memory = address$un_initialized_memory

   labeling  equ = current_free_address$un_initialized_memory
   preserve  equ current_free_address$un_initialized_memory = current_free_address$un_initialized_memory +
   #+end_src
** next
   #+begin_src fasm :tangle cicada-nymph.fasm
   macro next {
      pop_return_stack rbx
        mov rax, qword [rbx]
      add rbx, jo_size
      push_return_stack rbx
        jmp qword [rax]
   }
   #+end_src
** note play with jo & jojo
   1. at the beginning
      * argument-stack
        << 2 >>
      * return-stack
        #+begin_src return-stack
        - [ (square) ]
            (square)
            (end)
        #+end_src
   2. next
      * argument-stack
        << 2 >>
      * return-stack
        #+begin_src return-stack
            (square)
        - [ (square) ] - [ (dup) ]
            (end)          (multiple)
                           (end)
        #+end_src
   3. next
      * argument-stack
        << 2, 2 >>
      * return-stack
        #+begin_src return-stack
            (square)       (dup)
        - [ (square) ] - [ (multiple) ]
            (end)          (end)
        #+end_src
   4. next
      * argument-stack << 4 >>
      * return-stack
        #+begin_src return-stack
                           (dup)
            (square)       (multiple)
        - [ (square) ] - [ (end) ]
            (end)
        #+end_src
   5. next
      * argument-stack << 4 >>
      * return-stack
        #+begin_src return-stack
            (square)
            (square)
        - [ (end) ] - [ (dup) ]
                        (multiple)
                        (end)
        #+end_src
   6. next
      * argument-stack
        << 4, 4 >>
      * return-stack
        #+begin_src return-stack
            (square)
            (square)    (dup)
        - [ (end) ] - [ (multiple) ]
                        (end)
        #+end_src
   7. next
      * argument-stack
        << 16 >>
      * return-stack
        #+begin_src return-stack
            (square)    (dup)
            (square)    (multiple)
        - [ (end) ] - [ (end) ]
        #+end_src
   8. next
      * argument-stack
        << 16 >>
      * return-stack
        #+begin_src return-stack
            (square)
            (square)
        - [ (end) ]
        #+end_src
   9. next
      * argument-stack
        << 16 >>
      * return-stack
        #+begin_src return-stack
        - [  ]
        #+end_src
   10. it is really simple
       ^-^
       is it not ?
* -----------------------------------
* helper function in assembly code
** ----------------------------------
** __exit_with_TOS [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   __exit_with_TOS:
      pop_argument_stack sys_1_rdi
      mov sys_n_rax, syscall_exit
      syscall

   finish if
   #+end_src
** __exit_with_zero [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   __exit_with_zero:
      xor sys_1_rdi, sys_1_rdi
      mov sys_n_rax, syscall_exit
      syscall

   finish if
   #+end_src
** __exit_with_six [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   __exit_with_six:
      mov sys_1_rdi, 6
      mov sys_n_rax, syscall_exit
      syscall

   finish if
   #+end_src
** ----------------------------------
** note windows calling convention for x86-64
   * [[https://msdn.microsoft.com/en-us/library/ew5tede7%28v=vs.90%29.aspx][microsoft's web]]
   * if you respect the calling convention
     1. your functions will be able to call other functions
        which respect the calling convention
     2. your functions will be call-able by other functions
        which respect the calling convention
   * in our program
     1. we do NOT need to respect the calling convention
        to let our functions be call-able by other function
        which respect the calling convention
     2. we ONLY need to respect the calling convention
        to let our functions be able to call other functions
        which respect the calling convention
   * before a call to function in kernel
     you have to 16-byte aligne the stack
   * pass first 4 arguments by rcx rdx r8 r9
     pass other arguments by stack
   * you have to reserve 4 place for the first 4 arguments
     although you do not need to push them into stack
   * you have to reserve 4 place for the first 4 arguments
     even if the function you are calling
     only uses less then 4 arguments
   * the code I am using
     to handle windows calling convention
     is de-macro-lized and un-optimized
     this is for teaching purpose only
** __exit_with_TOS [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   __exit_with_TOS:
      number_of_arguments = 4
      push rbp
      mov rbp, rsp

      mov rax, rsp
      add rax, 8*number_of_arguments
      mov rbx, 1111b
      and rbx, rax

      sub rsp, 16
      add rsp, rbx

      sub rsp, 8*4
      pop_argument_stack fun_1_rcx
      call [ExitProcess]

      mov rsp, rbp
      pop rbp

   finish if
   #+end_src
** __exit_with_zero [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   __exit_with_zero:
      number_of_arguments = 4
      push rbp
      mov rbp, rsp

      mov rax, rsp
      add rax, 8*number_of_arguments
      mov rbx, 1111b
      and rbx, rax

      sub rsp, 16
      add rsp, rbx

      sub rsp, 8*4
      xor fun_1_rcx, fun_1_rcx
      call [ExitProcess]

      mov rsp, rbp
      pop rbp

   finish if
   #+end_src
** __exit_with_six [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   __exit_with_six:
      number_of_arguments = 4
      push rbp
      mov rbp, rsp

      mov rax, rsp
      add rax, 8*number_of_arguments
      mov rbx, 1111b
      and rbx, rax

      sub rsp, 16
      add rsp, rbx

      sub rsp, 8*4
      mov fun_1_rcx, 15
      call [ExitProcess]

      mov rsp, rbp
      pop rbp

   finish if
   #+end_src
** ----------------------------------
* -----------------------------------
* macro for jo & explainer
** ----------------------------------
** link
   #+begin_src fasm :tangle cicada-nymph.fasm
   ;; initial link to point to 0 (as null)
   link = 0
   #+end_src
** primitive_string_heap
   #+begin_src fasm :tangle cicada-nymph.fasm
   size$primitive_string_heap = 100 * 1024 ;; (byte)

   address$primitive_string_heap:
      times size$primitive_string_heap db 0

   current_free_address$primitive_string_heap = address$primitive_string_heap
   #+end_src
** make_primitive_string
   * 2 bytes for length of name_string
   * note that
     the following is using local label
   #+begin_src fasm :tangle cicada-nymph.fasm
   macro make_primitive_string string {

   virtual at 0
   .start$string:
      db string
   .end$string:
      dw (.end$string - .start$string)
      load .length word from (.end$string)
   finish virtual
   store word .length at (current_free_address$primitive_string_heap)

   current_free_address$primitive_string_heap = current_free_address$primitive_string_heap + 2

   repeat .length
      virtual at 0
         db string
         load .char byte from (% - 1)
      finish virtual
      store byte .char at (current_free_address$primitive_string_heap)
      current_free_address$primitive_string_heap = current_free_address$primitive_string_heap + 1
   finish repeat

   }
   #+end_src
** ----------------------------------
** note
   * note that
     after a "next" "jmp" to a explainer
     the "rax" stores the value of the jo to be explained
     so
     "rax" is used as an inexplicit argument
     of the following functions
   * explain$function is used as jojo-head
     and explains the meaning of the jojo as function
   * a jojo-head identifies one type of jo
** define_function
   #+begin_src fasm :tangle cicada-nymph.fasm
   macro define_function string, jo {

   define_function__#jo:

   name__#jo:
      xx current_free_address$primitive_string_heap

      make_primitive_string string

   link__#jo:
      xx link
      link = link__#jo

   jo:
      xx explain$function

      ;; here follows a jojo as function-body

      }
   #+end_src
** explain$function
   * find a jojo from a function-jo
     and push the jojo to return-stack
   * a jojo can not be of size 0
   * use rax as an argument
     which stores a jo
   #+begin_src fasm :tangle cicada-nymph.fasm
   explain$function:
      add rax, jo_size
      push_return_stack rax
      next
   #+end_src
** ----------------------------------
** note
   * primitive functions are special
     they explain themself
     and their type is not identified by jojo-head
** define_primitive_function
   #+begin_src fasm :tangle cicada-nymph.fasm
   macro define_primitive_function string, jo {

   define_primitive_function__#jo:

   name__#jo:
      xx current_free_address$primitive_string_heap

      make_primitive_string string

   link__#jo:
      xx link
      link = link__#jo

   jo:
      xx assembly_code__#jo

   assembly_code__#jo:

      ;; here follows assembly code
      ;; as primitive function body

      }
   #+end_src
** ----------------------------------
** note
   * no constant
     only variable
   * when a variable jo in the jojo
     it push the value of the variable to argument_stack
   * when wish to change a variable's value
     use key_word "address" to get the address of the variable
** define_variable
   #+begin_src fasm :tangle cicada-nymph.fasm
   macro define_variable string, jo {

   define_variable__#jo:

   name__#jo:
      xx current_free_address$primitive_string_heap

      make_primitive_string string

   link__#jo:
      xx link
      link = link__#jo

   jo:
      xx explain$variable

      ;; here follows a value of jo_size
      ;; only one value is allowed

      }
   #+end_src
** explain$variable
   #+begin_src fasm :tangle cicada-nymph.fasm
   explain$variable:
      add rax, jo_size
      mov rbx, [rax]
      push_argument_stack rbx
      next
   #+end_src
** ----------------------------------
* -----------------------------------
* end
** end
   #+begin_src fasm :tangle cicada-nymph.fasm
   define_primitive_function "end", end
      pop_return_stack rbx
      next
   #+end_src
* -----------------------------------
* *the story begin*
** begin_to_interpret_threaded_code [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   begin_to_interpret_threaded_code:

      cld ;; set DF = 0, then rsi and rdi are incremented

      mov pointer$argument_stack,  address$argument_stack
      mov pointer$return_stack,    address$return_stack

      mov rax, first_jojo
      push_return_stack rax
      next

   finish if
   #+end_src
** begin_to_interpret_threaded_code [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   ;;;; for windows
   _output_handle:
      dq 0
   _input_handle:
      dq 0
   ;;;;

   begin_to_interpret_threaded_code:

      cld ;; set DF = 0, then rsi and rdi are incremented


      ;;;; for windows
      number_of_arguments = 4
      push rbp
      mov rbp, rsp
      mov rax, rsp
      add rax, 8*number_of_arguments

      mov rbx, 1111b
      and rbx, rax

      sub rsp, 16
      add rsp, rbx


      sub rsp, 8*4
      mov fun_1_rcx, STD_INPUT_HANDLE
      call [GetStdHandle]
      mov [_input_handle], rax


      mov rsp, rbp
      pop rbp



      number_of_arguments = 4
      push rbp
      mov rbp, rsp

      mov rax, rsp
      add rax, 8*number_of_arguments
      mov rbx, 1111b
      and rbx, rax

      sub rsp, 16
      add rsp, rbx

      sub rsp, 8*4
      mov fun_1_rcx, STD_OUTPUT_HANDLE
      call [GetStdHandle]
      mov [_output_handle], rax

      mov rsp, rbp
      pop rbp
      ;;;;


      mov pointer$argument_stack,  address$argument_stack
      mov pointer$return_stack,    address$return_stack

      mov rax, first_jojo
      push_return_stack rax
      next

   finish if
   #+end_src
** first_jojo
   #+begin_src fasm :tangle cicada-nymph.fasm
   first_jojo:
      xx little_test
   #+end_src
** exit_with_TOS a.k.a. bye
   #+begin_src fasm :tangle cicada-nymph.fasm
   define_primitive_function "bye", exit_with_TOS
      call __exit_with_TOS
   #+end_src
** little_test
   #+begin_src fasm :tangle cicada-nymph.fasm
   define_variable "", V__little_test_number
      xx 3


   define_function "little_test", little_test

      ;;;; variable
      xx V__little_test_number
      xx exit_with_TOS
      ;;;; 3
   #+end_src
* -----------------------------------
* argument_stack
** memory allocation
   * for we do not build border-check
     into the interface of pop and push
     we allocation some memory below the stacks
   #+begin_src fasm :tangle cicada-nymph.fasm
      preserve 64 * jo_size
   address$argument_stack labeling
      preserve 1024 * 1024 * jo_size
   #+end_src
* return_stack
** memory allocation
   #+begin_src fasm :tangle cicada-nymph.fasm
      preserve 64 * jo_size
   address$return_stack labeling
      preserve 1024 * 1024 * jo_size
   #+end_src
* -----------------------------------
* epilog
** ----------------------------------
** un_initialized_memory [linux]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if linux? eq yes

   size$un_initialized_memory = 64 * 1024 * 1024 ;; (byte)

   segment readable writeable
   address$un_initialized_memory:
      rb size$un_initialized_memory

   finish if
   #+end_src
** ----------------------------------
** un_initialized_memory [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes

   size$un_initialized_memory = 64 * 1024 * 1024 ;; (byte)

   section '.data' data readable writeable
   address$un_initialized_memory:
      rb size$un_initialized_memory

   finish if
   #+end_src
** import [windows]
   #+begin_src fasm :tangle cicada-nymph.fasm
   if windows? eq yes


   ;; Macroinstructions for making import section (64-bit)

   macro library [name,string]
    { common
       import.data:
      forward
       local _label
       if defined name#.redundant
        if ~ name#.redundant
         dd RVA name#.lookup,0,0,RVA _label,RVA name#.address
        finish if
       finish if
       name#.referred = 1
      common
       dd 0,0,0,0,0
      forward
       if defined name#.redundant
        if ~ name#.redundant
         _label db string,0
                rb RVA $ and 1
        finish if
       finish if }

   macro import name,[label,string]
    { common
       rb (- rva $) and 7
       if defined name#.referred
        name#.lookup:
      forward
        if used label
         if string eqtype ''
          local _label
          dq RVA _label
         else
          dq 8000000000000000h + string
         finish if
        finish if
      common
        if $ > name#.lookup
         name#.redundant = 0
         dq 0
        else
         name#.redundant = 1
        finish if
        name#.address:
      forward
        if used label
         if string eqtype ''
          label dq RVA _label
         else
          label dq 8000000000000000h + string
         finish if
        finish if
      common
        if ~ name#.redundant
         dq 0
        finish if
      forward
        if used label & string eqtype ''
        _label dw 0
               db string,0
               rb RVA $ and 1
        finish if
      common
       finish if }


   section '.idata' import data readable writeable

   library kernel32,'KERNEL32.DLL'

   import kernel32,\
          ExitProcess,'ExitProcess',\
          ReadFile,'ReadFile',\
          WriteFile,'WriteFile',\
          GetStdHandle,'GetStdHandle'


   finish if
   #+end_src
** ----------------------------------
* ===================================
